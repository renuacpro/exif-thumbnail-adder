plugins {
    id 'com.android.application'
}

apply from: 'get-version.gradle'

def keystorePropertiesFile = rootProject.file("0_personal/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {

    compileSdkVersion 30
    buildToolsVersion "30.0.3"
    ndkVersion "22.1.7171670"

    defaultConfig {
        applicationId "com.exifthumbnailadder.app"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode finalVersionCode
        versionName finalVersionName
//        versionCode 194
//        versionName "0.9.10"

        file("../version_last_tag.txt").text = "version: " + lastTagVersionName + "+" + lastTagVersionCode
        //file("../version_head.txt").text = "version: " + finalVersionName + "+" + finalVersionCode

        // Add version to APK name
        if (project.hasProperty("noVersionInArchivesBaseName") && noVersionInArchivesBaseName == "true") {
            // APK name used in screengrab to build screenshots with fastlane is hardcoded
            // so we don't want the version no in the apk file
            setProperty("archivesBaseName", "exifthumbnailadder")
        } else {
            setProperty("archivesBaseName", "exifthumbnailadder-$versionName")
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // No automatic import :( [see: https://stackoverflow.com/a/54105322/15401262]
        buildConfigField "java.util.concurrent.atomic.AtomicBoolean", "IS_SCREENSHOTS", "new java.util.concurrent.atomic.AtomicBoolean(false)"

        externalNativeBuild {
            cmake {
//                abiFilters 'arm64-v8a'
//                abiFilters 'armeabi-v7a'
//                abiFilters 'x86'
//                abiFilters 'x86_64'
            }
        }
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            packagingOptions {
                //exclude 'doc/**'        // coming from pixymeta-android.jar
                //exclude 'changelog.txt' // coming from pixymeta-android.jar
            }
        }
        debug {
            //minifyEnabled true
            //shrinkResources true

            aaptOptions {
                //ignoreAssetsPattern '!**/doc:!**/changelog.txt'
            }
            packagingOptions {
                //exclude 'doc/**'        // coming from pixymeta-android.jar
                //exclude 'changelog.txt' // coming from pixymeta-android.jar
            }
        }
    }

    flavorDimensions "version"
    productFlavors {
        standard {
            // Without pixymeta-android
            getIsDefault().set(true)
        }
        google_play {
            // Without pixymeta-android
            // On play store, apps with MANAGE_EXTERNAL_STORAGE permission are limited.
            // On play store we publish this variant that is without MANGE_EXTERNAL_STORAGE and has targetSdk 29.
            // This permits to keep same behaviour as on Android 10 for devices running Android 11.
            targetSdkVersion 29
        }
        pixymeta {
            // With pixymeta-android library
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    externalNativeBuild {
        cmake {
            path file('../CMakeLists.txt')
        }
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.3.0' // Fix: https://stackoverflow.com/questions/51957944/android-view-viewonunhandledkeyeventlistener
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.fragment:fragment:1.3.4'
    implementation 'androidx.navigation:navigation-fragment:2.3.5'
    implementation 'androidx.navigation:navigation-ui:2.3.5'
    implementation 'androidx.exifinterface:exifinterface:1.3.2'
    implementation 'androidx.wear:wear:1.1.0'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.preference:preference:1.1.1'
    implementation project(path: ':library:Android-Exif-Extended')
    //implementation files('libs/*.jar')
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'tools.fastlane:screengrab:2.0.0'
    compileOnly 'com.google.android.wearable:wearable:2.8.1'

}

if (new File("enable-pixymeta-android.gradle").exists()) {
    apply from: 'enable-pixymeta-android.gradle'
}
